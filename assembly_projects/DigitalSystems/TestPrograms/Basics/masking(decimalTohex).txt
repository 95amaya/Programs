# Lecture 16 Demo Program 1, Hex Number Conversion
# Converts a Decimal Number to Its Equivalent Hex Value and
# prints out hexadecimal number to console. 
# $t0 - Loop counter
# $a0 - Holds each hex digit as analyzed
# $t2 - Holds number input from keyboard


		.data
prompt:		.asciiz "Enter a decimal number (8 digits max): "
answer: 	.asciiz "Hex number is: 0x"
comp:		.asciiz "Input another number (y.n)"

		

		.text
main:		la $a0,prompt
		li $v0,4
		syscall			#prompts the user to input a number

		li $v0,5
		syscall			#waits for user to enter a number

		move $t1,$v0
		la $a0,answer
		li $v0,4
		syscall
		
		move $t0,$0		#clears the counter

		li $v0,11

elim:		rol $t1,$t1,4
		and $a0,$t1,0xf
		bgtz $a0,num
		addi $t0,$t0,1
		beq $t0,8,zero
		j elim		

loop:		rol $t1,$t1,4
		and $a0,$t1,0xf
num:		ble $a0,9,conv
		add $a0,$a0,7
conv:		add $a0,$a0,48
		syscall
		addi $t0,$t0,1
		blt $t0,8,loop
		j next

zero:		li $a0,0x30 		
		syscall

next:		li $a0,0x0a
		syscall
		la $a0,comp
		li $v0,4
		syscall
		li $v0,11
		li $a0,0x0a
		syscall
		li $v0,12
		syscall
		beq $v0,0x79,main
		li $v0,10
		syscall


# EoF Lecture 16 Demo Program	